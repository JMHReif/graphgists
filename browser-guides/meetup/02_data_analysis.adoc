= Data Analysis
:data-url: https://raw.githubusercontent.com/neo4j-examples/graphgists/master/browser-guides/data
:img: https://s3.amazonaws.com/guides.neo4j.com/meetup/img
:gist: https://raw.githubusercontent.com/neo4j-examples/graphgists/master/browser-guides/meetup
:icons: font
:neo4j-version: 3.5

== Explore Meetup data with Cypher

We can dig deeper into our graph to gain insights using the relationships between entities for insights.

First, let's see which cities have the most events.

*Remember:* We imported random data from api, so there may be varying results.

[source,cypher]
----
//find cities with most events
MATCH (c:City)-[n:NEAR]-(v:Venue)-[l:LOCATED_AT]-(e:Event)
RETURN c.name, count(e) as count ORDER BY count DESC;
----

== Analysis: Topics

=== Query if any groups are in the Tech topic
[source,cypher]
----
//note: there may not be any groups for this topic in our database due to random import
MATCH (t:Topic {name: 'Tech'})-[r:TOPIC]-(g:Group)
RETURN *;
----

=== Find most popular topics

[source,cypher]
----
MATCH (t:Topic)-[r:TOPIC]-(g:Group)
RETURN t.name, count(g) as count ORDER BY count DESC
----

== Analysis: Topics

=== Find which users attend the most Meetups in a random topic

[source,cypher]
----
MATCH (t:Topic) 
WITH collect(t) as topics 
WITH apoc.coll.randomItem(topics) as targetTopic
MATCH (targetTopic)-[:TOPIC]-(g:Group)-[:HELD]-(e:Event)<-[:EVENT]-(r:RSVP)-[:MEMBER]-(member:Member)
RETURN targetTopic.name as topic, member.name as member, count(r) as RSVPs
ORDER BY RSVPs DESC limit 10;
----

== Analysis: Groups

=== Which group was created most recently?

[source,cypher]
----
MATCH (g:Group)
RETURN g
ORDER BY g.created DESC
LIMIT 1
----

=== How many groups have been running for at least 1 year?

[source,cypher]
----
//note: there may not be any results due to random import
MATCH (g:Group)
WHERE (timestamp() - g.created) / 1000 / 3600 / 24 / 365 >= 1
RETURN count(g)
----

== Analysis: Groups

=== Find groups with 'Neo4j' or 'Data' in their name.

[source,cypher]
----
MATCH (g:Group)
WHERE g.name CONTAINS 'Neo4j' OR g.name CONTAINS 'Data'
RETURN g
----

=== What are the distinct topics for those groups?

[source,cypher]
----
MATCH (g:Group)-[:TOPIC]->(t:Topic)
WHERE g.name CONTAINS 'Neo4j' OR g.name CONTAINS 'Data'
RETURN t.name, count(*)
----

== Analysis: Events

=== Who brings the most guests?

[source,cypher]
----
MATCH (r:RSVP)-[:MEMBER]->(m:Member)
WHERE r.guests > 5
RETURN m.name, sum(r.guests) as totalGuests
ORDER BY totalGuests DESC limit 10;
----

=== Which venue hosts the most meetups?

[source,cypher]
----
MATCH (v:Venue)<-[:LOCATED_AT]-(e:Event)
WHERE v.name IS NOT NULL
RETURN v.name, v.location, count(e) as events
ORDER BY events desc 
LIMIT 10;
----

== Analysis: Events

=== Find meetups a random venue has hosted?

[source,cypher]
----
MATCH (v:Venue)
WHERE v.name IS NOT NULL
WITH collect(v) as venues
WITH apoc.coll.randomItem(venues) as venue
MATCH (venue)<-[:LOCATED_AT]-(e:Event)<-[:HELD]-(g:Group),
 (e)-[:EVENT]-(r:RSVP)
RETURN venue.name, venue.location, e.name, g.name, count(r) as RSVPs
LIMIT 10;
----

== Analysis: Shortest paths

=== Shortest paths between random venues

[source,cypher]
----
MATCH (v:Venue)
WHERE v.name IS NOT NULL
WITH collect(v) as venues
WITH apoc.coll.randomItem(venues) as v1, apoc.coll.randomItem(venues) as v2
MATCH p=shortestPath((v1)-[*]-(v2))
RETURN p;
----

=== Shortest path between two random topics

[source,cypher]
----
MATCH (t:Topic)
WITH collect(t) as topics
WITH apoc.coll.randomItem(topics) as t1, apoc.coll.randomItem(topics) as t2
MATCH p=shortestPath((t1)-[*]-(t2))
RETURN p;
----

== Analysis: Shortest paths

=== Shortest path among 3 random members

[source,cypher]
----
MATCH (m:Member)
WITH collect(m) as members
WITH apoc.coll.randomItem(members) as m1, apoc.coll.randomItem(members) as m2, apoc.coll.randomItem(members) as m3
MATCH p1=shortestPath((m1)-[*]-(m2)),
      p2=shortestPath((m2)-[*]-(m3)),
      p3=shortestPath((m1)-[*]-(m3))
RETURN p1, p2, p3;
----

== Analysis: Find events in area

=== Find future Richmond meetups within 10 miles of downtown

[source,cypher]
----
WITH point({ latitude: 37.5407246, longitude: -77.4360481 }) as RichmondVA, 32186.9 as TenMiles   /* 10 mi expressed in meters */
MATCH (v:Venue)<-[:LOCATED_AT]-(e:Event)-[:HELD]-(g:Group) 
WHERE distance(v.location, RichmondVA) < TenMiles AND e.time > datetime()
RETURN g.name as GroupName, e.name as EventName, e.time as When, v.name as Venue limit 10;
----

== Analysis: Find events in area

=== Find events within distance of random location

[source,cypher]
----
WITH rand() * 90 * (CASE WHEN rand() <= 0.5 THEN 1 ELSE -1 END) as randLat, rand() * 90 * (CASE WHEN rand() <= 0.5 THEN 1 ELSE -1 END) as randLon
WITH point({ latitude: randLat, longitude: randLon }) as randomLocation
MATCH (v:Venue)-[:NEAR]->(city:City)-[:IN]->(c:Country)
RETURN city.name as City, 
    c.name as Country, 
    v.name as Venue, 
    v.location as VenueLocation, 
    randomLocation as RandomLocation,
    distance(v.location, randomLocation) as DistanceInMeters
ORDER BY distance(v.location, randomLocation) ASC
LIMIT 1;
----

== Analysis: Find events in area

=== Find upcoming dance events in Manhattan

[source,cypher]
----
WITH point({ latitude: 40.758896, longitude: -73.985130 }) as TimesSquareManhattan, 32186.9 as TenMiles
MATCH (v:Venue)<-[:LOCATED_AT]-(e:Event),
      (e)-[:HELD]-(g:Group),
      (g)-[:TOPIC]->(t:Topic),
      (e)<-[:EVENT]-(r:RSVP)
WHERE e.time >= datetime("2018-09-06T00:00:00Z") AND
      e.time <= datetime("2018-09-06T23:59:59Z") AND
      distance(v.location, TimesSquareManhattan) < TenMiles AND
      v.name is not null AND
      t.name =~ '(?i).*dancing.*'
RETURN g.name as GroupName, 
    collect(distinct t.name) as topics, 
    e.name as EventName, 
    count(r) as RSVPs, 
    e.time as When, 
    v.name as Venue 
ORDER BY RSVPs DESC
LIMIT 100;
----

== Next

We have seen how to use Cypher to import and analyze meetup data from the Meetup API.
We can continue analysis with additional queries, import other data for more layers, and more!